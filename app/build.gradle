import java.text.SimpleDateFormat

plugins {
  id "com.google.protobuf" version "0.8.16"
  id 'com.android.application'
  id 'kotlin-android'
  id 'kotlin-kapt'
  id 'kotlin-parcelize'
  id 'androidx.navigation.safeargs.kotlin'
  id 'idea'
  id 'jacoco'
  id 'com.google.firebase.firebase-perf'
}

static buildTime() {
  def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
  df.setTimeZone(TimeZone.getTimeZone("UTC"))
  return df.format(new Date())
}

def gitHash() {
  return "git -C ${rootDir} rev-parse --short HEAD".execute().text.trim()
}

def version = "1.5.1"
def version_code = 124

android {
  compileSdkVersion 30
  buildToolsVersion "30.0.3"

  buildFeatures {
    viewBinding true
  }

  defaultConfig {
    applicationId "com.kelsos.mbrc"
    minSdkVersion 23
    targetSdkVersion 30
    versionCode version_code
    versionName version
    buildConfigField "String", "GIT_SHA", "\"${gitHash()}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""

    // Enabling multidex support.
    multiDexEnabled true

    testInstrumentationRunner 'com.kelsos.mbrc.MockTestRunner'
    testApplicationId "com.kelsos.mbrc.test"
    vectorDrawables.useSupportLibrary = true

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
    execution 'ANDROIDX_TEST_ORCHESTRATOR'
  }

  compileOptions {
    // Flag to enable support for the new language APIs
    coreLibraryDesugaringEnabled true
    // Sets Java compatibility to Java 8
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = "1.8"
    freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi", "-Xopt-in=kotlin.RequiresOptIn"]
  }

  dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
  }

  signingConfigs {
    release {
      storeFile file('placeholder')
      keyAlias "placeholder"
      storePassword "placeholder"
      keyPassword "placeholder"
    }
  }

  flavorDimensions "basic"

  buildTypes {
    release {
      signingConfig signingConfigs.release
      debuggable false
      zipAlignEnabled true
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      buildConfigField "String", "GIT_SHA", "\"${gitHash()}\""
      buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
    }

    debug {
      applicationIdSuffix ".dev"
      versionNameSuffix "-dev"
      testCoverageEnabled false

      buildConfigField "String", "GIT_SHA", "\"debug_build\""
      buildConfigField "String", "BUILD_TIME", "\"debug_build\""
    }
  }

  productFlavors {
    play {
      apply plugin: 'com.google.gms.google-services'
      apply plugin: 'com.google.firebase.crashlytics'
    }

    github {}
  }


  packagingOptions {
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/services/javax.annotation.processing.Processor'
    exclude "**/module-info.class"
    pickFirst("META-INF/atomicfu.kotlin_module")
  }

  lintOptions {
    abortOnError false
    lintConfig file("lint.xml")
    xmlReport false
    htmlReport true
    htmlOutput file("$project.buildDir/reports/lint/lint-result.html")
    xmlOutput file("$project.buildDir/reports/lint/lint-result.xml")
  }

  dexOptions {
    javaMaxHeapSize "2G"
  }

  android.applicationVariants.all { variant ->
    variant.outputs.all {
      outputFileName = "${defaultConfig.applicationId}-${variant.flavorName}-${variant.versionCode}-v${variant.versionName}.apk"
    }
  }
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

dependencies {
  implementation project(path: ':changelog')
  implementation 'androidx.viewpager2:viewpager2:1.0.0'

  testImplementation "io.insert-koin:koin-test:2.2.2"

  testImplementation "androidx.test:runner:1.4.0"
  testImplementation "androidx.test.ext:junit:1.1.3"
  testImplementation "androidx.test.espresso:espresso-intents:3.4.0"
  testImplementation "androidx.test.espresso:espresso-core:3.4.0"
  testImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
  testImplementation "androidx.test:core:1.4.0"
  testImplementation "androidx.room:room-testing:2.3.0"

  testImplementation "androidx.test.ext:truth:1.4.0"
  testImplementation "androidx.arch.core:core-testing:2.1.0"
  testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0'

  testImplementation "io.mockk:mockk:1.12.0"
  testImplementation "com.google.truth:truth:1.1.3"

  testImplementation "org.robolectric:robolectric:4.6"
  testImplementation "org.robolectric:shadows-multidex:4.6"

  //The android library only will fail the robolectric tests
  testImplementation 'org.threeten:threetenbp:1.5.1'

  implementation 'com.google.android.material:material:1.4.0'

  implementation 'androidx.core:core-ktx:1.6.0'
  implementation 'androidx.appcompat:appcompat:1.3.0'
  implementation 'androidx.media:media:1.3.1'

  implementation "androidx.fragment:fragment-ktx:1.3.5"
  implementation 'androidx.annotation:annotation:1.2.0'
  implementation 'androidx.recyclerview:recyclerview:1.2.1'
  implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
  implementation 'androidx.preference:preference-ktx:1.1.1'

  implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
  implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"

  def nav_version = "2.3.5"
  implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
  implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

  implementation "androidx.paging:paging-runtime-ktx:3.0.0"

  implementation 'androidx.room:room-runtime:2.3.0'
  implementation "androidx.room:room-ktx:2.3.0"
  kapt 'androidx.room:room-compiler:2.3.0'

  implementation 'com.squareup.moshi:moshi:1.12.0'
  kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.12.0'

  implementation 'io.insert-koin:koin-android:2.2.2'
  implementation 'io.insert-koin:koin-androidx-viewmodel:2.2.2'
  implementation 'io.insert-koin:koin-androidx-ext:2.2.2'
  implementation 'io.insert-koin:koin-androidx-scope:2.2.2'

  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'

  implementation 'com.squareup.picasso:picasso:2.8'
  implementation 'androidx.multidex:multidex:2.0.1'

  implementation 'com.jakewharton.timber:timber:4.7.1'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

  implementation 'com.jakewharton.threetenabp:threetenabp:1.3.1'

  implementation "androidx.datastore:datastore:1.0.0-rc01"
  implementation "com.google.protobuf:protobuf-javalite:3.17.3"

  implementation "com.squareup.okio:okio:2.10.0"
  def arrow_version = "0.8.1"
  implementation "io.arrow-kt:arrow-core:$arrow_version"
  implementation "io.arrow-kt:arrow-syntax:$arrow_version"
  implementation "io.arrow-kt:arrow-typeclasses:$arrow_version"
  implementation "io.arrow-kt:arrow-data:$arrow_version"
  implementation "io.arrow-kt:arrow-instances-core:$arrow_version"
  implementation "io.arrow-kt:arrow-instances-data:$arrow_version"
  kapt    "io.arrow-kt:arrow-annotations-processor:$arrow_version"

  debugImplementation "androidx.fragment:fragment-testing:1.3.5"
  debugImplementation "com.squareup.leakcanary:leakcanary-android:2.7"

  // Import the BoM for the Firebase platform
  playImplementation platform('com.google.firebase:firebase-bom:28.2.0')

  // Declare the dependencies for the Crashlytics and Analytics libraries
  // When using the BoM, you don't specify versions in Firebase library dependencies
  playImplementation 'com.google.firebase:firebase-crashlytics-ktx'
  playImplementation 'com.google.firebase:firebase-analytics-ktx'
  playImplementation 'com.google.firebase:firebase-perf-ktx'
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.17.3"
  }

  // Generates the java Protobuf-lite code for the Protobufs in this project. See
  // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
  // for more information.
  generateProtoTasks {
    all().each { task ->
      task.builtins {
        java {
          option 'lite'
        }
      }
    }
  }
}


allprojects {
  afterEvaluate { project ->

    def signProperties = rootProject.file('signing.properties')
    if (signProperties.exists()) {
      def prop = new Properties()
      prop.load(new FileInputStream(signProperties))
      def filepath = prop["keys"]
      def keyProperties = file(filepath)
      if (keyProperties.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(keyProperties))
        def keystorePath = props['keystore']
        def releaseConfig = android.signingConfigs['release']
        releaseConfig.storeFile = file(keystorePath)
        releaseConfig.storePassword = props['storePass']
        releaseConfig.keyAlias = props['keyAlias']
        releaseConfig.keyPassword = props['keyPass']
      }
    }
  }
}

repositories {
  mavenCentral()
}

jacoco {
  toolVersion = "0.8.7"
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testGithubDebugUnitTest']) {
  group = "Reporting"
  description "Generate jacoco coverage reports"

  def fileFilter = [
    '**/R.class',
    '**/R$*.class',
    '**/BuildConfig.*',
    '**/Manifest*.*',
    '**/*Test*.*',
    '**/**_Impl**',
    '**/*$*$*.*', // Anonymous classes generated by kotlin
    'android/**/*.*',
    'androidx/**/*.*',
    '**/databinding/**/*.class',
    '**/**/*JsonAdapter.class',
    '**/DataBinderMapperImpl**',
    '**/DataBindingInfo**',
    '**/BR.*',
    '**/*$inlined$*.*', // Kotlin specific, Jacoco can not handle several "$" in class name.
  ]

  def debugTree = fileTree(
    dir: "${buildDir}/intermediates/javac/githubDebug/compileGithubDebugJavaWithJavac",
    excludes: fileFilter
  )
  def kotlinTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/githubDebug")

  reports {
    xml.enabled true
    html.enabled true
    csv.enabled false
    classDirectories.setFrom(files([debugTree, kotlinTree]))
  }

  def mainSrc = "${project.projectDir}/src/main/java"

  sourceDirectories.setFrom(files([mainSrc]))
  executionData.setFrom(fileTree(dir: "$buildDir", includes: [
    "**/*.exec",
    "**/*.ec"
  ]))
}

tasks.withType(Test) {
  jacoco.includeNoLocationClasses = true
  jacoco.excludes = ['jdk.internal.*']
}

configurations.all {
  resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
  resolutionStrategy.force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
  resolutionStrategy.force "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}
