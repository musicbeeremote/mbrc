import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

repositories {
  maven { url 'https://maven.fabric.io/public' }
}

static buildTime() {
  def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
  df.setTimeZone(TimeZone.getTimeZone("UTC"))
  return df.format(new Date())
}

def gitHash() {
  return "git -C ${rootDir} rev-parse --short HEAD".execute().text.trim()
}

def version = "1.2.0"
def version_code = 114

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.2"

  defaultConfig {
    applicationId "com.kelsos.mbrc"
    minSdkVersion 16
    targetSdkVersion 25
    versionCode version_code
    versionName version
    buildConfigField "String", "GIT_SHA", "\"${gitHash()}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""

    // Enabling multidex support.
    multiDexEnabled true

    testInstrumentationRunner 'com.kelsos.mbrc.MockTestRunner'
    testApplicationId "com.kelsos.mbrc.test"
  }

  testOptions {
    unitTests.returnDefaultValues = true
    unitTests.all {
      jacoco {
        includeNoLocationClasses = true
      }
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  signingConfigs {
    release {
      storeFile file('placeholder')
      keyAlias "placeholder"
      storePassword "placeholder"
      keyPassword "placeholder"
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
      debuggable false
      zipAlignEnabled true
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      buildConfigField "String", "GIT_SHA", "\"${gitHash()}\""
      buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
      buildConfigField "boolean", "USE_CRASHLYTICS", "true"
    }

    debug {
      applicationIdSuffix ".dev"
      versionNameSuffix "-dev"
      testCoverageEnabled true
      ext.enableCrashlytics = false

      buildConfigField "String", "GIT_SHA", "\"debug_build\""
      buildConfigField "String", "BUILD_TIME", "\"debug_build\""
      buildConfigField "boolean", "USE_CRASHLYTICS", "false"
    }
  }

  productFlavors {
    play {
      ext.enableCrashlytics = true
    }

    github {
      ext.enableCrashlytics = false
    }
  }


  packagingOptions {
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }

  lintOptions {
    abortOnError false
    lintConfig file("lint.xml")
    xmlReport false
    htmlReport true
    htmlOutput file("$project.buildDir/reports/lint/lint-result.html")
    xmlOutput file("$project.buildDir/reports/lint/lint-result.xml")
  }

  dexOptions {
    javaMaxHeapSize "2G"
  }

  applicationVariants.all { variant ->
    variant.outputs.each { output ->
      def originalFilename = output.outputFile
      def versionInfo = "-v${variant.mergedFlavor.versionCode}-${variant.mergedFlavor.versionName}"
      def filename = originalFilename.name
          .replace(".apk", versionInfo + ".apk")
          .replace(project.name, defaultConfig.applicationId)
      filename = "${originalFilename.parent}/${filename}"
      output.outputFile = file(filename)
    }
  }

  sourceSets {
    androidTest.java.srcDirs += "src/androidTest/kotlin"
    test.java.srcDirs += 'src/test/kotlin'
    main.java.srcDirs += 'src/main/kotlin'
    debug.java.srcDirs += 'src/debug/kotlin'
    play.java.srcDirs += 'src/play/kotlin'
  }
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

ext {
  support_library_version = "25.3.1"
  dbflow_version = "4.0.0-beta7"
  toothpick_version = "1.0.6"
  jackson_version = "2.8.8"
  butterknife_version = "8.5.1"
  material_dialogs_version = "0.9.4.4"
  runner_version = "0.5"
  espresso_version = "2.2.2"
  junit_version = "4.12"
  mockito_version = "2.8.9"
  robolectric_version = "3.3.2"
  truth_version = "0.32"
  leakcanary_version = "1.5.1"
}

dependencies {
  //Instrumentation Test dependencies
  androidTestCompile("com.android.support.test.espresso:espresso-core:${espresso_version}", {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  androidTestCompile "com.android.support.test.espresso:espresso-intents:${espresso_version}"
  androidTestCompile "junit:junit:${junit_version}"

  androidTestCompile "org.mockito:mockito-core:${mockito_version}"
  androidTestCompile "org.mockito:mockito-android:${mockito_version}"

  androidTestCompile "com.github.stephanenicolas.toothpick:toothpick-testing:${toothpick_version}"
  androidTestCompile "com.android.support:support-annotations:${support_library_version}"
  androidTestCompile "com.android.support.test:runner:${runner_version}"
  androidTestCompile "com.android.support.test:rules:${runner_version}"
  androidTestCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanary_version}"
  androidTestCompile 'com.linkedin.testbutler:test-butler-library:1.3.0'

  // Test dependencies
  testCompile "junit:junit:${junit_version}"
  testCompile "org.mockito:mockito-core:${mockito_version}"
  testCompile "org.robolectric:robolectric:${robolectric_version}"
  testCompile "org.robolectric:shadows-multidex:${robolectric_version}"
  testCompile "com.google.truth:truth:${truth_version}"
  testCompile "com.github.stephanenicolas.toothpick:toothpick-testing:${toothpick_version}"
  testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanary_version}"
  //The android library only will fail the robolectric tests
  testCompile 'org.threeten:threetenbp:1.3.3'

  compile "com.android.support:appcompat-v7:${support_library_version}"
  compile "com.android.support:support-v4:${support_library_version}"
  compile "com.android.support:support-annotations:${support_library_version}"
  compile "com.android.support:recyclerview-v7:${support_library_version}"
  compile "com.android.support:design:${support_library_version}"
  compile "com.android.support:preference-v7:${support_library_version}"
  compile "com.android.support:preference-v14:${support_library_version}"

  //Some of the dependencies pull the wrong version support v13
  compile "com.android.support:support-v13:${support_library_version}"

  kapt "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"

  compile "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
  compile "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"
  compile "com.github.Raizlabs.DBFlow:dbflow-kotlinextensions:${dbflow_version}@aar"
  compile "com.github.Raizlabs.DBFlow:dbflow-rx2:${dbflow_version}"
  compile "com.github.Raizlabs.DBFlow:dbflow-rx2-kotlinextensions:${dbflow_version}"

  compile "com.fasterxml.jackson.core:jackson-core:${jackson_version}"
  compile "com.fasterxml.jackson.core:jackson-databind:${jackson_version}"
  compile "com.fasterxml.jackson.module:jackson-module-kotlin:${jackson_version}"
  compile "com.jakewharton:butterknife:${butterknife_version}"

  compile "com.github.stephanenicolas.toothpick:toothpick-runtime:${toothpick_version}"
  compile "com.github.stephanenicolas.toothpick:smoothie:${toothpick_version}"
  kapt "com.github.stephanenicolas.toothpick:toothpick-compiler:${toothpick_version}"

  compile "com.afollestad.material-dialogs:core:${material_dialogs_version}"
  compile "com.afollestad.material-dialogs:commons:${material_dialogs_version}"
  compile 'com.github.gabrielemariotti.changeloglib:changelog:2.1.0'

  kapt "com.jakewharton:butterknife-compiler:${butterknife_version}"
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  compile 'com.android.support:multidex:1.0.1'
  compile 'com.squareup.picasso:picasso:2.5.2'

  compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
  compile 'io.reactivex.rxjava2:rxjava:2.1.0'
  compile 'io.reactivex.rxjava2:rxkotlin:2.0.2'
  compile 'uk.co.chrisjenx:calligraphy:2.2.0'
  compile 'com.jakewharton.timber:timber:4.5.1'
  compile 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
  compile 'com.android.support.constraint:constraint-layout:1.0.2'

  compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'

  debugCompile "com.squareup.leakcanary:leakcanary-android:${leakcanary_version}"
  releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanary_version}"

  playCompile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
    transitive = true
  }
}

allprojects {
  afterEvaluate { project ->

    def signProperties = rootProject.file('signing.properties')
    if (signProperties.exists()) {
      def prop = new Properties()
      prop.load(new FileInputStream(signProperties))
      def filepath = prop.getAt("keys")
      def keyProperties = file(filepath)
      if (keyProperties.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(keyProperties))
        def keystorePath = props.getAt('keystore')
        def releaseConfig = android.signingConfigs['release']
        releaseConfig.storeFile = file(keystorePath)
        releaseConfig.storePassword = props.getAt('storePass')
        releaseConfig.keyAlias = props.getAt('keyAlias')
        releaseConfig.keyPassword = props.getAt('keyPass')
      }
    }
  }
}

afterEvaluate {
  initFabricPropertiesIfNeeded()
}

/**
 * For CI because otherwise the build will fail on the fabric plugin*/
def initFabricPropertiesIfNeeded() {
  def propertiesFile = file('fabric.properties')
  if (!propertiesFile.exists()) {
    def commentMessage = "This is autogenerated fabric property from system environment to prevent key to be committed to source control."
    ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
      entry(key: "apiSecret", value: crashlyticsBuildSecret)
      entry(key: "apiKey", value: crashlyticsApikey)
    }
  }
}

coveralls {
  jacocoReportPath = "${buildDir}/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
}

tasks.coveralls {
  dependsOn 'jacocoTestReport'
  onlyIf { System.env.'CI' }
}

repositories {
  mavenCentral()
}

kapt {
  generateStubs = true
  arguments {
    arg('toothpick_registry_package_name', 'com.kelsos.mbrc')
  }
}

task jacocoTestReport(type: JacocoReport,
    dependsOn: ['testGithubDebugUnitTest', 'createGithubDebugCoverageReport']) {
  group = "Reporting"
  description "Generate jacoco coverage reports"

  reports {
    xml.enabled = true
    html.enabled = true
  }

  def fileFilter = ['**/R.class',
                    '**/R$*.class',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    '**/*Test*.*',
                    '**/FactoryRegistry.*',
                    '**/*_ViewBinding*',
                    '**/*MemberInjector*',
                    '**/*$$Factory*.*',
                    '**/MemberInjectorRegistry.*',
                    '**/**_Table.*',
                    '**/GeneratedDatabaseHolder.*',
                    '**/*_Database.*',
                    '**/*$*$*.*', // Anonymous classes generated by kotlin
                    'android/**/*.*']
  def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/github/debug",
      excludes: fileFilter)
  def mainSrc = "${project.projectDir}/src/main/kotlin"

  sourceDirectories = files([mainSrc])
  classDirectories = files([debugTree])
  executionData = fileTree(dir: "$buildDir", includes: ["**/*.exec",
                                                        "**/*.ec"])
}
